apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId 'com.ryanst.app'
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        ryanstConfig {
            storeFile file(RELEASE_STOREFILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.ryanstConfig
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.outputFile != null && output.outputFile.name.endsWith('.apk')
                            && 'release'.equals(variant.buildType.name)) {
                        def apkFile = new File(
                                output.outputFile.getParent(),
                                "ryanst${variant.flavorName}_v${variant.versionName}_${buildTime()}.apk")
                        output.outputFile = apkFile
                    }
                }
            }
        }

        debug {
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
//            testCoverageEnabled = true
            debuggable true
            signingConfig signingConfigs.ryanstConfig
        }
    }

    //输入参数中包含 -Pmc 时才进行多渠道构建
    if (project.hasProperty('mc')) {
        def path = "./build-types/type.txt"
        //多渠道构建
        println "multi channel"
        file(path).eachLine { line ->
            println "channel name : " + line
            android.productFlavors.create(line, {
                manifestPlaceholders = [CHANNEL_VALUE: "" + line]
            })
        }
    } else {
        buildTypes.release.manifestPlaceholders = [CHANNEL_VALUE: "999"]
        buildTypes.debug.manifestPlaceholders = [CHANNEL_VALUE: "999"]
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

buildscript {
    repositories {
        maven { url "http://repo1.maven.org/maven2" }
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'

        //hugo 函数log
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'

repositories {
    maven { url "https://jitpack.io" }
    maven { url "http://repo1.maven.org/maven2" }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':ryanstlib')
    compile 'com.android.support:support-v4:23.2.0'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    //junit
    testCompile 'junit:junit:4.12'
    //glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.1'
    //butterknife
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    //supertoast
    compile 'com.github.johnpersano:supertoasts:1.3.4@aar'
    //logger
    compile 'com.github.orhanobut:logger:1.12'
    //leakcanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    //blockcanary
    debugCompile 'com.github.moduth:blockcanary-android:1.2.1'
    releaseCompile 'com.github.moduth:blockcanary-no-op:1.2.1'
    //eventbus
    compile 'org.greenrobot:eventbus:3.0.0'

    //友盟统计
    compile 'com.umeng.analytics:analytics:latest.integration'

    /**
     *  Because RxAndroid releases are few and far between, it is recommended you also
     *  explicitly depend on RxJava's latest version for bug fixes and new features.
     */

    //rxJava
    compile 'io.reactivex:rxjava:1.1.6'
    //rxandroid
    compile 'io.reactivex:rxandroid:1.2.1'
    //rxpermisiion
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
    //rxBinding
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0'
}

def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
